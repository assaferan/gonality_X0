/////////////////This code proves that the gonality of X_0(109) is 5.
load "new_models.m";
X,ws,pairs:= eqs_quos(109,[[109]]);
Xw:=pairs[1,1];
f:=pairs[1,2];
ptsXw:=PointSearch(Xw,100);
#ptsXw;
pb:=[Pullback(f,Place(p)):p in ptsXw];
pls1:={}; pls2:={};
for i:=1 to #pb do
dec:=Decomposition(pb[i]);
if #dec eq 2 then pls1:= pls1 join {dec[1,1], dec[2,1]}; else  pls2:= pls2 join {dec[1,1]};
end if;
end for;
[Degree(P): P in pls1];
[Degree(P): P in pls2];
Max([Dimension(RiemannRochSpace(p+q+r+s+t)) : p,q,r,s,t in pls1]);
Max([Dimension(RiemannRochSpace(p+q+r+s)) : p,q,r in pls1, s in pls2]);
Max([Dimension(RiemannRochSpace(p+q+r)) : p in pls1, r,q in pls2]);
//We find a function of degree 5! Hence the upper bound for the gonality is 5.




X:= eqs_quos(109,[]);
D:=DefiningEquations(X);
D2:=[];
for i:=1 to #D do
p:=D[i];
l:=LCM([Denominator(a):a in Coefficients(p)]);
p:=p*l;
D2:=D2 cat [p];
end for;
C:=Scheme(ProjectiveSpace(Rationals(),Genus(X)-1),D2);
C2:=ChangeRing(C,GF(3));
FF := FunctionField(C2);
AFF := AlgorithmicFunctionField(FF);
pls1:=Places(AFF,1);
pls2:=Places(AFF,2);
#pls1,#pls2;  
// 4, 13; Hence there is a function with polar divisor having at most 1 F_3-rational point 


//4+0
pls4:=Places(AFF,4);
s:={};
for k:=1 to #pls4 do
s:=s  join {Dimension(RiemannRochSpace(pls4[k]))};
end for;
s;
//returns {1} nad F_5;

//3+1
pls3:=Places(AFF,3);
s:={};
for k:=1 to #pls3 do
for m:=1 to #pls1 do
s:=s  join {Dimension(RiemannRochSpace(pls3[k]+pls1[m]))};
end for;
end for;
s;
//returns 1

//2+2
s:={};
for k:=1 to #pls2 do
for m:=1 to #pls2 do
s:=s  join {Dimension(RiemannRochSpace(pls2[k]+pls2[m]))};
end for;
end for;
s;
//returns 1

//2*1+2
s:={};
for k:=1 to #pls1 do
for m:=1 to #pls2 do
s:=s  join {Dimension(RiemannRochSpace(2*pls1[k]+pls2[m]))};
end for;
end for;
s;
//returns 1

//4*1
s:={};
for p in pls1 do 
s:=s  join {Dimension(RiemannRochSpace(4*p))};
end for;
s;
//returns 1
//Conclusion: no deg 4 functions over F_3
